MODULE LocationReport;

REQUIRE ItemDelivery;

NAMESPACE Inventory;

FORM currentStockPrices 'Current stock and prices'
    TREE locations lt = Location PARENT parent(lt)
    PROPERTIES READONLY name(lt)
    ORDERS name(lt)

    TREE categories c = Category PARENT parent(c)
    PROPERTIES READONLY name(c)
    ORDERS name(c)
    FILTERGROUP activeCategory
        FILTER '{Active}' active(c) DEFAULT

    OBJECTS p = Product

    PROPERTIES(p) READONLY id, 'Product' = name, nameUom
    PROPERTIES  price(p, lt), promotionPrice(p, lt), promotionFrom(p, lt), promotionTo(p, lt), dateTimeUpdatedPrice(p, lt), balance(p, lt), dateTimeUpdatedBalance(p, lt)
    FILTERGROUP location
        FILTER 'Active' price(p, lt) AND balance(p, lt) DEFAULT
        FILTER 'With price' price(p, lt)
        FILTER 'On hand' balance(p, lt) > 0
;


DESIGN currentStockPrices {
    OBJECTS {
        NEW invReport {
            fill = 1;
            tabbed = TRUE;
            NEW pane {
                caption = 'Inventory report';
                horizontal = TRUE;
                fill = 1;
                NEW filterTabs {
                    fill = 1;
                    tabbed = TRUE;
                    MOVE BOX(TREE locations) { caption = 'Locations'; }
                    MOVE BOX(TREE categories) { caption = 'Categories'; }
                }
                NEW pane2 {
                    fill = 3;
                    MOVE BOX(p) { caption = ''; }
                }
            }
        }
    }
}

NAVIGATOR {
    reporting{
        NEW currentStockPrices;
    }
}